?? functions commonly used in PPCG submissions

??<<
??? Returns a list of inputs and outputs for <code>func</code> and <code>tc</code>.
??? @type func fn
??? @type tc [(*)]
??? @return [[(*)]]
??? @genre string
ExecuteTestCases[func, tc] := V#func => tc
??>>

??<<
??? Returns a list of strings of formatted test cases of the form <code>input -> output</code>.
??? @type func fn
??? @type tc [(*)]
??? @return [string]
??? @genre string
FormatTestCases[func, tc] :=
    {Format["%s -> %s", _, func[_]]} => tc
??>>

??<<
??? Prints each string in <code>FormatTestCases[...args]</code>.
??? @type args (*)...
??? @return [[string]]
??? @genre IO
PrintTestCases := Print => FormatTestCases
??>>

??<<
??? Displays each test case.
??? @type func fn
??? @type tc [(*)]
??? @return [[(*)]]
??? @genre IO
DisplayTestCases := {
    Print[Repr[_1], "->"]
    Display[_2]
} &> ExecuteTestCases
??>>

??<<
??? Formats <code>code</code> as a PPCG submission.
??? @type code string
??? @return string
??? @genre string
Submission[code] :=
    Format["# Attache, %s bytes\n\n    %s\n", #code, code]
??>>

??<<
??? Prints <code>Submission[code]</code>.
??? @type code string
??? @return [string]
??? @genre IO
PrintSubmission := Print@Submission
??>>
